buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.0.0"
group = "network.indexyz.minecraft.coolq"
archivesBaseName = "coolq-minecraft"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.4.2705"
    runDir = "run"
    
    mappings = "snapshot_20171003"
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.9.1'
    compile 'org.json:json:20171018'
    compile 'com.squareup.okio:okio:1.14.0'
    compile 'org.nanohttpd:nanohttpd:2.3.1'
}

shadowJar {
    dependencies {
        include dependency('com.squareup.okhttp3:okhttp:3.9.1')
        include dependency('org.json:json:20171018')
        include dependency('com.squareup.okio:okio:1.14.0')
        include dependency('org.nanohttpd:nanohttpd:2.3.1')
    }
    classifier = 'full'
}

build.dependsOn(shadowJar)

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

reobf { shadowJar { mappingType = 'SEARGE' } }
tasks.reobfShadowJar.mustRunAfter shadowJar
